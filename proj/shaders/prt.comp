#version 450


layout(local_size_x=1, local_size_y=1, local_size_z=1) in;

layout(set=0, binding=0) buffer ShCoeffs
{
	vec4 shCoeffs[16];
} sb0;
layout(set=1, binding=0) uniform textureCube cubeMap;
layout(set=2, binding=0) uniform sampler clampSampler;

float evalSH9(vec3 dir, int index);
vec3 getCubemapDirection(int face, float u, float v);
float texelSolidAngle(vec3 dir);
float atan2(in float y, in float x)
{
    return x == 0.0 ? sign(y)*3.141592f/2 : atan(y, x);
}

vec3 ACESFilm(vec3 x)
{
	const float a = 2.51;
	const float b = 0.03;
	const float c = 2.43;
	const float d = 0.59;
	const float e = 0.14;
	return clamp((x*(a*x + b)) / (x*(c*x + d) + e), 0.0, 1.0);
}

void main()
{
	uvec3 gtid = gl_GlobalInvocationID;
	
	vec3 coeff[9];
	
	for (int i = 0; i < 9; i++)
	{
		coeff[i] = vec3(0.0f);
	}

	for (int face = 0; face < 6; face++)
	{
		for (int y = 0; y < CUBEMAP_SIZE; y++)
		{
			for (int x = 0; x < CUBEMAP_SIZE; x++)
			{
				float u = float(x) / CUBEMAP_SIZE * 2.0f - 1.0f;
				float v = float(y) / CUBEMAP_SIZE * 2.0f - 1.0f;
			
				vec3 dir = normalize(getCubemapDirection(face, u, v));
			
				vec3 color = texture(samplerCube(cubeMap, clampSampler), dir).rgb;
				
				//color = ACESFilm(color);
			
				for (int i = 0; i < 9; i++)
				{
					float sh = evalSH9(dir, i);
			
					float b = sh * texelSolidAngle(getCubemapDirection(face, u, v));
				
					coeff[i] += color * b;
				}
			}
		}
	}

	for (int i = 0; i < 9; i++)
	{
		//coeff[i] *= 1.0f / (CUBEMAP_SIZE * CUBEMAP_SIZE * 1);
		sb0.shCoeffs[i] = vec4(coeff[i], 0.0f);
	}
}


float evalSH9(vec3 dir, int index)
{
	float x = dir.x;
	float y = dir.y;
	float z = dir.z;
	
	float sh[9];
	
	sh[0] = 0.282095f;
	
	sh[1] = 0.488603f * y;
	sh[2] = 0.488603f * z;
	sh[3] = 0.488603f * x;
	
	sh[4] = 1.092548f * x * y;
	sh[5] = 1.092548f * y * z;
	sh[6] = 0.315392f * (3.0f * z * z - 1.0f);
	sh[7] = 1.092548f * x * z;
	sh[8] = 0.546274f * (x * x - y * y);
	
	return sh[index];
}

vec3 getCubemapDirection(int face, float u, float v)
{
	if (face == 0) return (vec3(1, -v, -u));
	if (face == 1) return (vec3(-1, -v, u));
	if (face == 2) return (vec3(u, 1, v));
	if (face == 3) return (vec3(u, -1, -v));
	if (face == 4) return (vec3(u, -v, 1));
	if (face == 5) return (vec3(-u, -v, -1));
	
	return vec3(0.0f);
}

float texelSolidAngle(vec3 dir)
{
	float length2 = dot(dir, dir);

	return (4.0f * 3.141592f) / (6.0f * CUBEMAP_SIZE * CUBEMAP_SIZE) * pow(length2, -1.5f);
}

